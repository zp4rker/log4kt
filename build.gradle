plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.0'

    id 'maven'
    id 'signing'

    id 'io.codearte.nexus-staging' version '0.22.0'
}

archivesBaseName = project.name
group 'com.zp4rker'
version '1.1.7'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib"

    compile "org.slf4j:slf4j-api:1.7.30"
    compile "org.fusesource.jansi:jansi:2.1.1"

    testCompile 'junit:junit:4.11'
}

compileKotlin {
    kotlinOptions.jvmTarget = 1.8
}

compileTestKotlin {
    kotlinOptions.jvmTarget = 1.8
}

task javadocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment d -> signing.signPom(d) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name "Log4Kt"
                artifactId "log4kt"
                packaging "jar"
                description "A very simple SLF4J implementation written in Kotlin."
                url "https://github.com/zp4rker/log4kt"

                scm {
                    connection "scm:git:git://github.com/zp4rker/log4kt.git"
                    url "https://github.com/zp4rker/log4kt"
                }

                licenses {
                    license {
                        name "The Apache License, Version 2.0"
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id "zp4rker"
                        name "Zaeem Parker"
                        email "iam@zp4rker.com"
                    }
                }
            }
        }
    }
}